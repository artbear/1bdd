Перем ИсполнительБДД;
Перем ВозможныеСтатусыВыполнения;
Перем ТипыСостоянияJUnit;

// Сформировать лог-файл проверки в формате Ant.JUnit
//
// Параметры:
//   РезультатыВыполнения - ДеревоЗначение
//   СтатусВыполнения - СтатусыВыполнения
//   ПутьОтчетаJUnit - Строка - путь к лог-файлу проверки в формате Ant.JUnit
//
Процедура Сформировать(Знач РезультатыВыполнения, Знач СтатусВыполнения, Знач ПутьОтчетаJUnit) Экспорт
    // ДатаНачала = Неопределено;
    ИсполнительБДД = Новый ИсполнительБДД;
    ВозможныеСтатусыВыполнения = ИсполнительБДД.ВозможныеСтатусыВыполнения();
    
    ЧитательГеркин = Новый ЧитательГеркин;
    ВозможныеТипыШагов = ЧитательГеркин.ВозможныеТипыШагов();
    
    ТипШага_Функциональность = ВозможныеТипыШагов.Функциональность; 
    МассивШагов = Новый Массив;
    МассивШагов.Добавить(ТипШага_Функциональность);
    МассивШагов.Добавить(ВозможныеТипыШагов.Сценарий);
    СтруктураИтогов = ИсполнительБДД.ПолучитьИтоговыеРезультатыВыполнения(РезультатыВыполнения, МассивШагов);
    
    ЗаписьXML = Новый ЗаписьXML;
    ЗаписьXML.УстановитьСтроку("UTF-8");
    ЗаписьXML.ЗаписатьОбъявлениеXML();
    
    Итоги = СтруктураИтогов[ТипШага_Функциональность];
    
    КоличествоОшибок = Итоги[ВозможныеСтатусыВыполнения.Сломался];
    КоличествоНереализованныхТестов = Итоги[ВозможныеСтатусыВыполнения.НеРеализован] +
        Итоги[ВозможныеСтатусыВыполнения.НеВыполнялся];
    ВсегоТестов = Итоги[ВозможныеСтатусыВыполнения.Пройден] 
				+ КоличествоОшибок + КоличествоНереализованныхТестов;
    
    // // ВремяВыполнения = ТекущаяДата() - ДатаНачала;
    ВремяВыполнения = 0;//TODO вычислять время выполнения прогона фич, шагов и сценариев
    
    ЗаписьXML.ЗаписатьНачалоЭлемента("testsuites");
    ЗаписьXML.ЗаписатьАтрибут("name", XMLСтрока("1bdd"));
    ЗаписьXML.ЗаписатьАтрибут("time", XMLСтрока(ВремяВыполнения));
    ЗаписьXML.ЗаписатьАтрибут("tests", XMLСтрока(ВсегоТестов));
    ЗаписьXML.ЗаписатьАтрибут("failures", XMLСтрока(КоличествоОшибок));
    ЗаписьXML.ЗаписатьАтрибут("skipped", XMLСтрока(КоличествоНереализованныхТестов)); // или disabled
    
    Для каждого Узел Из РезультатыВыполнения.Строки Цикл
        ЗаписьXML.ЗаписатьНачалоЭлемента("testsuite");	
        
        ЗаписьXML.ЗаписатьАтрибут("name", Узел.Тело);
        
        ЗаписьXML.ЗаписатьНачалоЭлемента("properties");
        ЗаписьXML.ЗаписатьКонецЭлемента();
        
        Для Каждого УзелСценария Из Узел.Строки Цикл
            Если УзелСценария.ТипШага <> ВозможныеТипыШагов.Сценарий Тогда
        		Продолжить;    
			КонецЕсли;
			Для Каждого УзелШага Из УзелСценария.Строки Цикл
				Если УзелШага.ТипШага = ВозможныеТипыШагов.Шаг Тогда
					ЗаполнитьРезультатТестовогоСлучая(ЗаписьXML, УзелСценария, УзелШага);
					Если УзелШага.СтатусВыполнения <> ВозможныеСтатусыВыполнения.Пройден Тогда
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;	
        КонецЦикла;	
        
        ЗаписьXML.ЗаписатьКонецЭлемента();
    КонецЦикла;
    
    ЗаписьXML.ЗаписатьКонецЭлемента();
    
    СтрокаХМЛ = ЗаписьXML.Закрыть();
    
    ФайлОтчета = Новый Файл(ОбъединитьПути(ТекущийКаталог(), ПутьОтчетаJUnit));
    
    ЗаписьXML = Новый ЗаписьXML;
    ЗаписьXML.ОткрытьФайл(ФайлОтчета.ПолноеИмя);
    ЗаписьXML.ЗаписатьБезОбработки(СтрокаХМЛ);// таким образом файл будет записан всего один раз, и не будет проблем с обработкой на билд-сервере TeamCity
    ЗаписьXML.Закрыть();
    Сообщить(" ");
	Сообщить(СтрШаблон("Путь к лог-файлу проверки в формате Ant.JUnit <%1>", ФайлОтчета.ПолноеИмя));
    
КонецПроцедуры

Процедура ЗаполнитьРезультатТестовогоСлучая(ЗаписьXML, Знач УзелСценария, Знач УзелШага)
    
    ЗаписьXML.ЗаписатьНачалоЭлемента("testcase");
    ЗаписьXML.ЗаписатьАтрибут("classname", УзелСценария.Тело);
    ЗаписьXML.ЗаписатьАтрибут("name", УзелШага.Тело);
    
    ТипСостоянияJUnit = ПолучитьТипыСостоянияJUnit()[УзелШага.СтатусВыполнения];
    ЗаписьXML.ЗаписатьАтрибут("status", ТипСостоянияJUnit);
    
    Если УзелШага.СтатусВыполнения = ВозможныеСтатусыВыполнения.Сломался Тогда
        ЗаписьXML.ЗаписатьНачалоЭлемента(ТипСостоянияJUnit);
        ОписаниеОшибкиВыполнения = УзелШага.ОписаниеОшибкиВыполнения;
		Сообщить(СтрШаблон("ОписаниеОшибкиВыполнения %1", ОписаниеОшибкиВыполнения));
        // TODO: НайтиНедопустимыеСимволыXML()
        XMLОписаниеОшибкиВыполнения = XMLСтрока(ОписаниеОшибкиВыполнения); 
        ЗаписьXML.ЗаписатьАтрибут("message", XMLОписаниеОшибкиВыполнения);
        ЗаписьXML.ЗаписатьКонецЭлемента();
    КонецЕсли;
    
    ЗаписьXML.ЗаписатьКонецЭлемента();
    
КонецПроцедуры

Функция ПолучитьТипыСостоянияJUnit()
    Если ТипыСостоянияJUnit = Неопределено Тогда
        
        ТипыСостоянияJUnit = Новый Соответствие;
        ТипыСостоянияJUnit.Вставить(ВозможныеСтатусыВыполнения.Пройден, "passed");
        ТипыСостоянияJUnit.Вставить(ВозможныеСтатусыВыполнения.Сломался, "failure");
        ТипыСостоянияJUnit.Вставить(ВозможныеСтатусыВыполнения.НеРеализован, "skipped");
        ТипыСостоянияJUnit.Вставить(ВозможныеСтатусыВыполнения.НеВыполнялся, "skipped");
        ТипыСостоянияJUnit = Новый ФиксированноеСоответствие(ТипыСостоянияJUnit);
    КонецЕсли;
    Возврат ТипыСостоянияJUnit;
КонецФункции // ПолучитьТипыСостоянияJUnit()
