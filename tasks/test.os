// если в командной строке скрипту передать ключ "coverage", будет выполнен расчет покрытия кода

#Использовать ".."
#Использовать 1testrunner
#Использовать fs

Перем ОписаниеПокрытия;

Функция ПрогнатьТесты(Знач ПутьКТестам = "tests", Знач ПутьОтчетаХмл = ".")

	Тестер = Новый Тестер;

	Если ОписаниеПокрытия.ИспользуетсяПокрытиеКода Тогда
		Тестер.ВключитьСборСтатистикиСкриптовOnescript(Новый Файл(ОписаниеПокрытия.ПутьКаталогаФайловПокрытия));
		Тестер.УстановитьФорматЛогФайла(Тестер.ФорматыЛогФайла().GenericExec);
	КонецЕсли;

	ПутьОтчетаХмл = Новый Файл(ПутьОтчетаХмл).ПолноеИмя;

	РезультатТестирования = Тестер.ТестироватьКаталог(
		Новый Файл(ПутьКТестам),
		Новый Файл(ПутьОтчетаХмл)
	);

	Успешно = РезультатТестирования = 0;

	Возврат Успешно;
КонецФункции

Функция ПрогнатьФичи(Знач ПутьФич = "features", Знач ПутьОтчетаХмл = "./bdd-log.xml")

	ИсполнительБДД = Новый ИсполнительБДД;
	Возврат ИсполнительБДД.ВыполнитьФичиИзКоманднойСтроки(ОписаниеПокрытия, ПутьФич, ПутьОтчетаХмл);

КонецФункции

Функция НайтиПараметрыПокрытия(Знач ИмяКаталогаФайловПокрытия = "coverage")

	ПутьКаталогаФайловПокрытия = "";

	ИспользуетсяПокрытиеКода = Ложь;
	Для каждого Элемент Из АргументыКоманднойСтроки Цикл
		Если Элемент = "coverage" Тогда

			ПутьКаталогаФайловПокрытия = ОбъединитьПути(ТекущийКаталог(), ".", ИмяКаталогаФайловПокрытия);
			ФС.ОбеспечитьПустойКаталог(ПутьКаталогаФайловПокрытия);

			ИспользуетсяПокрытиеКода = Истина;

			Прервать;
		КонецЕсли;
	КонецЦикла;

	Результат = Новый Структура;
	Результат.Вставить("ИспользуетсяПокрытиеКода", ИспользуетсяПокрытиеКода);
	Результат.Вставить("ПутьКаталогаФайловПокрытия", ПутьКаталогаФайловПокрытия);

	Возврат Результат;
КонецФункции

// основной код

ТекКаталог = ТекущийКаталог();

ОписаниеПокрытия = НайтиПараметрыПокрытия();

Попытка
	ТестыПрошли = ПрогнатьТесты();
Исключение
	ТестыПрошли = Ложь;
	Сообщить(СтрШаблон("Тесты через 1testrunner выполнены неудачно
	|%1
	|%2", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()), ОписаниеОшибки()));
КонецПопытки;

УстановитьТекущийКаталог(ТекКаталог);

Попытка
	ФичиПрошли = ПрогнатьФичи("features/core");
Исключение
	ФичиПрошли = Ложь;
	Сообщить(СтрШаблон("Тесты поведения через 1bdd выполнены неудачно
	|%1", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
КонецПопытки;

Попытка
	БиблиотечныеФичиПрошли = ПрогнатьФичи("features/lib", "bdd-lib.xml");
Исключение
	БиблиотечныеФичиПрошли = Ложь;
	Сообщить(СтрШаблон("Тесты поведения через 1bdd выполнены неудачно
	|%1", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
КонецПопытки;

Сообщить(СтрШаблон("Результат прогона тестов <%1>
|", ТестыПрошли));
Сообщить(СтрШаблон("Результат прогона основных фич <%1>
|", ФичиПрошли));
Сообщить(СтрШаблон("Результат прогона библиотечных фич <%1>
|", БиблиотечныеФичиПрошли));

Если НЕ ТестыПрошли Или НЕ ФичиПрошли Или НЕ БиблиотечныеФичиПрошли Тогда
	ВызватьИсключение "Тестирование завершилось неудачно!";
КонецЕсли;
